@AbapCatalog.viewEnhancementCategory: [#NONE]
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Agency Entity'
@Metadata.ignorePropagatedAnnotations: true
@ObjectModel.usageType:{
    serviceQuality: #X,
    sizeCategory: #S,
    dataClass: #MIXED
}
define view entity ZEY_AO_U_AGENCY as select from /dmo/agency as Agency
association[1] to I_Country as _Country on
$projection.CountryCode = _Country.Country
{
    key agency_id as AgencyId,
    name as Name,
    street as Street,
    postal_code as PostalCode,
    city as City,
    country_code as CountryCode,
    phone_number as PhoneNumber,
    email_address as EmailAddress,
    web_address as WebAddress,
    _Country
}



@AbapCatalog.viewEnhancementCategory: [#NONE]
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Customer Master data'
@Metadata.ignorePropagatedAnnotations: true
@ObjectModel.usageType:{
    serviceQuality: #X,
    sizeCategory: #S,
    dataClass: #MIXED
}
define view entity ZEY_AO_U_CUSTOMER as select from /dmo/customer as Customer 
association[1] to I_Country as _Country on
$projection.CountryCode = _Country.Country
{
    key customer_id as CustomerId,
    first_name as FirstName,
    last_name as LastName,
    title as Title,
    street as Street,
    postal_code as PostalCode,
    city as City,
    country_code as CountryCode,
    phone_number as PhoneNumber,
    email_address as EmailAddress,
    _Country
}



@AbapCatalog.viewEnhancementCategory: [#NONE]
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Root View for Travel Un managed scenario'
@Metadata.ignorePropagatedAnnotations: true
@ObjectModel.usageType:{
    serviceQuality: #X,
    sizeCategory: #S,
    dataClass: #MIXED
}
@Metadata.allowExtensions: true
define view entity ZEY_AO_I_U_TRAVEL as select from /dmo/travel as Travel 
association[1] to ZEY_AO_U_AGENCY as _Agency on 
$projection.AgencyId = _Agency.AgencyId
association[1] to ZEY_AO_U_CUSTOMER as _Customer on 
$projection.CustomerId = _Customer.CustomerId
association[1] to I_Currency as _Currency on 
$projection.CurrencyCode = _Currency.Currency
association[1] to /DMO/I_Travel_Status_VH as _TravelStatus on 
$projection.Status = _TravelStatus.TravelStatus
{   
    @ObjectModel.text.element: ['Description']
    key Travel.travel_id as TravelId,
    @ObjectModel.text.element: ['AgencyName']
    Travel.agency_id as AgencyId,
    _Agency.Name as AgencyName,
    @ObjectModel.text.element: ['CustomerName']
    Travel.customer_id as CustomerId,
    _Customer.FirstName as CustomerName,
    Travel.begin_date as BeginDate,
    Travel.end_date as EndDate,
    @Semantics.amount.currencyCode: 'CurrencyCode'
    Travel.booking_fee as BookingFee,
    @Semantics.amount.currencyCode: 'CurrencyCode'
    Travel.total_price as TotalPrice,
    Travel.currency_code as CurrencyCode,
    Travel.description as Description,
    @ObjectModel.text.element: ['StatusText']
    Travel.status as Status,
    _TravelStatus._Text[Language = $session.system_language ].TravelStatus as StatusText,
    Travel.createdby as Createdby,
    Travel.createdat as Createdat,
    Travel.lastchangedby as Lastchangedby,
    Travel.lastchangedat as Lastchangedat,
    _Agency,
    _Customer,
    _Currency,
    _TravelStatus
}




@EndUserText.label: 'Unamanged Scenario Travel SD'
define service ZEY_AO_SD_U_TRAVEL {
  expose ZEY_AO_I_U_TRAVEL as Travel;
  expose ZEY_AO_U_AGENCY as Agency;
  expose ZEY_AO_U_CUSTOMER as Customer;
  expose I_Currency as Currecny;
  expose I_Country as Country;
  expose /DMO/I_Travel_Status_VH as TravelStatus;
}


@Metadata.layer: #CORE
@UI.headerInfo:{ 
    typeName: 'Travel',
    typeNamePlural: 'Travel',
    description: {value: 'TotalPrice' },
    title: {value: 'Description' }
}
@UI.presentationVariant: [{ sortOrder: [{ by: 'AgencyId', direction: #DESC }] }]
@Search.searchable: true
annotate view ZEY_AO_I_U_TRAVEL
    with 
{   
    @UI.facet: [{
                    id: 'Travel',
                    purpose: #STANDARD,
                    type: #IDENTIFICATION_REFERENCE,
                    label: 'Travel Details',
                    position: 10
     }]
    @UI.selectionField: [{position: 10 }]
    @UI.lineItem: [{position: 10, label: 'Travel ID' }]
    @UI.identification: [{position: 10 }]
    @Search.defaultSearchElement: true
    TravelId;
    @UI.selectionField: [{position: 20 }]
    @UI.lineItem: [{position: 20, label: 'Agency ID' }]
    @UI.identification: [{position: 20 }]
    @Consumption.valueHelpDefinition: [{ entity: { name: 'ZEY_AO_U_AGENCY', element: 'AgencyId' } }]
    @Search.defaultSearchElement: true
    AgencyId;
    @UI.selectionField: [{position: 30 }]
    @UI.lineItem: [{position: 30, label: 'Customer ID' }]
    @UI.identification: [{position: 30 }]
    @Consumption.valueHelpDefinition: [{ entity: { name: 'ZEY_AO_U_CUSTOMER', element: 'CustomerId' } }]
    @Search.defaultSearchElement: true
    CustomerId;
    @UI.lineItem: [{position: 40 }]
    @UI.identification: [{position: 40 }]
    BeginDate;
//    EndDate;
//    BookingFee;
    @UI.lineItem: [{position: 50 }]
    @UI.identification: [{position: 50 }]
    TotalPrice;
    @UI.lineItem: [{position: 60 }]
    @UI.identification: [{position: 60 }]
    CurrencyCode;
//    @UI.lineItem: [{position: 70 }]
    @UI.identification: [{position: 70 }]
    Description;
    @UI.lineItem: [{position: 80 }]
    @UI.identification: [{position: 80 }]
    Status;
//    Createdby;
//    Createdat;
//    Lastchangedby;
//    Lastchangedat;
    
}