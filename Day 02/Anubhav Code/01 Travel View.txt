@AbapCatalog.viewEnhancementCategory: [#NONE]
@AccessControl.authorizationCheck: #CHECK
@EndUserText.label: 'Travel CDS view - Unmanaged BO'
@Metadata.ignorePropagatedAnnotations: true
@ObjectModel.usageType:{
    serviceQuality: #X,
    sizeCategory: #S,
    dataClass: #MIXED
}
@Search.searchable: true

@Metadata.allowExtensions: true
define view entity ZI_BKL_XX_U_TRAVEL as select from /dmo/travel as Travel
association[1] to ZI_BKL_XX_U_AGENCY as _Agency on
$projection.AgencyId = _Agency.AgencyId
association[1] to ZI_BKL_XX_U_CUSTOMER as _Customer on
$projection.CustomerId = _Customer.CustomerId
association[1] to I_Currency as _Currency on
$projection.CurrencyCode = _Currency.Currency
association[1..1] to /DMO/I_Travel_Status_VH as _TravelStatus
on $projection.Status = _TravelStatus.TravelStatus
{
    @Search.defaultSearchElement: true
    @ObjectModel.text.element: ['Description']
    key Travel.travel_id as TravelId,
    @Consumption.valueHelpDefinition: [{entity:{ name: 'ZI_BKL_XX_U_AGENCY', element: 'AgencyId'} }]
    @EndUserText.label: 'Agency'
    
    @Search.defaultSearchElement: true
    @ObjectModel.text.element: ['AgencyName']
    Travel.agency_id as AgencyId,
    
    _Agency.Name as AgencyName,
    
    @Consumption.valueHelpDefinition: [{entity:{ name: 'ZI_BKL_XX_U_CUSTOMER', element: 'CustomerId'} }]
    @EndUserText.label: 'Customer ID'
    @Search.defaultSearchElement: true
    @ObjectModel.text.element: ['CustomerName']
    Travel.customer_id as CustomerId,    
    
    concat(_Customer.FirstName, _Customer.LastName) as CustomerName,    
    Travel.begin_date as BeginDate,
    Travel.end_date as EndDate,
    @Semantics.amount.currencyCode: 'CurrencyCode'
    Travel.booking_fee as BookingFee,
    @Semantics.amount.currencyCode: 'CurrencyCode'
    Travel.total_price as TotalPrice,
    Travel.currency_code as CurrencyCode,
    Travel.description as Description,
    @Consumption.valueHelpDefinition: [{entity:{ name: '/DMO/I_Travel_Status_VH', element: 'TravelStatus'} }]
    @ObjectModel.text.element: ['TravelStatusText']
    Travel.status as Status,
    case status
        when 'N' then 3 --green 
        when 'B' then 2 --yellow
        when 'X' then 1 --red
        else 0
    end as Criticality,
    _TravelStatus._Text.Text as TravelStatusText,
    Travel.createdby as Createdby,
    Travel.createdat as Createdat,
    Travel.lastchangedby as Lastchangedby,
    Travel.lastchangedat as Lastchangedat,
    _Agency,
    _Customer,
    _Currency,
    _TravelStatus
}
